// Copyright (c) 2019 Lightricks. All rights reserved.
// Created by Nir Moshe

/**
 * Avoid parallelism during connectedAndroidTests.
 *
 * Espresso doesnâ€™t handle multi-project repos. Therefore we executes connectedAndroidTest one by one.
 * The following code builds a "chain" of `connectedAndroidTest`, this creates a serial execution.
 */
gradle.projectsEvaluated {
    // Collect all the connectedAndroidTest tasks.
    List<Task> connectedTestsArray = new ArrayList<>()

    allprojects.each { project ->
        for (Task task : project.tasks.findAll { it.name.matches("connected.+AndroidTest") }) {
            connectedTestsArray.push(task)
        }
    }

    // Create dependency between each connectedAndroidTest.
    for (int i = 0; i < connectedTestsArray.size() - 1; i++) {
        connectedTestsArray[i].mustRunAfter(connectedTestsArray[i + 1])
        println connectedTestsArray[i].toString() + " mustRunAfter " + connectedTestsArray[i + 1].toString()
    }
}
